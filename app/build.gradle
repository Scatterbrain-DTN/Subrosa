plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id "androidx.navigation.safeargs.kotlin"
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.protobuf'
}
def protobuf_version = "3.11.1"

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "net.ballmerlabs.subrosa"
        minSdkVersion 29
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.incremental": "true",
                        "room.schemaLocation": "$projectDir/schemas".toString()
                ]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }
}

sourceSets {
    main.java.srcDirs +=  "${protobuf.generatedFilesBaseDir}/main/javalite"
    main.java.srcDirs += "$projectDir/src/main/proto"
}

processResources {
    exclude('**/*.proto')
}


protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protobuf_version"
    }
    plugins {
        javalite {
            artifact = "com.google.protobuf:protoc-gen-javalite:$protobuf_version"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
    implementation 'com.github.lelloman:android-identicons:v11'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation "androidx.room:room-runtime:2.4.0-alpha04"
    implementation "com.sergivonavi:materialbanner:1.2.0"
    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation "com.google.protobuf:protobuf-javalite:$protobuf_version"
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    kapt "androidx.room:room-compiler:2.4.0-alpha04"
    implementation "androidx.room:room-ktx:2.4.0-alpha04"
    testImplementation "androidx.room:room-testing:2.4.0-alpha04"
    testImplementation 'junit:junit:4.+'
    implementation 'com.google.dagger:dagger:2.35'
    kapt 'com.google.dagger:dagger-compiler:2.35'
    implementation project(':scatterbrainSDK')
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

repositories {
    mavenCentral()
}